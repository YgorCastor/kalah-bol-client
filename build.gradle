buildscript {
	dependencies {
		classpath "org.openapitools:openapi-generator-gradle-plugin:4.0.3"
	}
}

plugins {
	id 'java'
	id 'groovy'
	id 'maven'
	id 'net.researchgate.release' version '2.7.0'
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

install {
	repositories.mavenInstaller {
		pom.artifactId = 'kalah-client'
	}
}

task execute(type: JavaExec) {
	main = System.getProperty('mainClass')
	classpath = sourceSets.main.runtimeClasspath
}

ext {
	swagger_annotations_version = "1.5.15"
	jackson_version = "2.9.8"
	jersey_version = "2.25.1"
	jodatime_version = "2.9.9"
	junit_version = "4.12"
}

dependencies {
	// Default generated client dependencies
	compile "io.swagger:swagger-annotations:$swagger_annotations_version"
	compile "org.glassfish.jersey.core:jersey-client:$jersey_version"
	compile "org.glassfish.jersey.media:jersey-media-multipart:$jersey_version"
	compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jersey_version"
	compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
	compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
	compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jackson_version"
	compile "joda-time:joda-time:$jodatime_version"
	compile 'com.brsanthu:migbase64:2.2'
	compile 'io.swagger.core.v3:swagger-annotations:2.0.6'
	testCompile "junit:junit:$junit_version"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

task generateSwaggerCodeAngular(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, group: 'build') {
	generatorName = "typescript-angular"
	inputSpec = "swagger.api.yaml"
	skipValidateSpec = true
	additionalProperties = [
		modelPropertyNaming: "original",
	]
	outputDir = "$project.projectDir.path/src/main/angular".toString()
}

task generateSwaggerCodeJava(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, group: 'build') {
	generatorName = "java"
	inputSpec = "swagger.api.yaml"
	skipValidateSpec = true

	additionalProperties = [
		modelPackage           : "kalah.bol.client.domain",
		apiPackage             : "kalah.bol.client.api",
		invokerPackage         : "kalah.bol.client",
		groupId                : "kalah.bol",
		artifactId             : "kalah-client",
		dateLibrary            : "joda",
		library                : "jersey2",
		hideGenerationTimestamp: "true",
	]
	outputDir = project.projectDir.path
}

task generateSwaggerCode(dependsOn: ['generateSwaggerCodeAngular', 'generateSwaggerCodeJava'], group: 'build')

compileJava.dependsOn generateSwaggerCode

afterReleaseBuild.dependsOn uploadArchives

release {
	git {
		requireBranch = ''
	}
}
